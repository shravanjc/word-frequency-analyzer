plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.anva'
version = '0.0.1-SNAPSHOT'
description = 'Simple spring boot application to calculate word frequency efficiently'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'net.jqwik:jqwik:1.9.1'
}

tasks.named('test') {
    useJUnitPlatform()
}

sourceSets {
    itest {
        java.srcDir 'src/itest/java'
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    itestImplementation.extendsFrom testImplementation
    itestRuntimeOnly.extendsFrom testRuntimeOnly
    itestAnnotationProcessor.extendsFrom annotationProcessor
    itestCompileOnly.extendsFrom compileOnly
}

tasks.register('itest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.itest.output.classesDirs
    classpath = sourceSets.itest.runtimeClasspath
    useJUnitPlatform()
    shouldRunAfter tasks.named('test')
}

jacoco {
    toolVersion = "0.8.14"
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
    }
}
